//@version=5
strategy("[다이버전스 1강] 다이버전스 전략", overlay=false, process_orders_on_close = true, initial_capital = 1000000, default_qty_type = strategy.percent_of_equity, default_qty_value = 100)
// 1. 지표
rsi = ta.rsi(close, 14)
plot(rsi, linewidth = 2)
// 2. 피봇 찾기 (상승div-피봇로우, 하락div-피봇하이)
pivotlow = ta.pivotlow(rsi, 5, 5) 
// plot(pivotlow, color = color.green, linewidth = 3, style = plot.style_linebr, offset = -5)
// 3. 다이버전스 찾기
is_pivotlow = not na(pivotlow)
// regular 상승다이버전스 => 지표 피봇저점은 상승, 가격 저점은 하락 
prev_pivotlow = ta.valuewhen(is_pivotlow, pivotlow, 1)
osc_higher_low =  is_pivotlow ? pivotlow > prev_pivotlow : false 
// bgcolor(osc_higher_low ? color.rgb(76, 175, 79, 90) : na, offset = -5)
prev_low = ta.valuewhen(is_pivotlow, low[5], 1)
price_lower_low = is_pivotlow ? low[5] < prev_low : false
// bgcolor(price_lower_low ? color.rgb(255, 153, 0, 90) : na, offset = -5)
is_regular_bullish_divergence = osc_higher_low and price_lower_low
// bgcolor(is_regular_bullish_divergence ? color.green : na, offset = -5)
plotshape(is_regular_bullish_divergence ? pivotlow : na, offset = -5, title="상승 다이버전스", text = "Bull", style = shape.labelup, color = color.green, textcolor = color.white, location = location.absolute)
plot(is_pivotlow ? pivotlow : na, color = is_regular_bullish_divergence ? color.green : na, offset = -5, linewidth = 2)

// 4. 전략 진입 | 종료
is_long = is_regular_bullish_divergence
is_long_close = ta.crossover(rsi, 70)

if is_long
    strategy.entry("롱", strategy.long)
if is_long_close
    strategy.close("롱")
