//@version=5
strategy("다이버전스 전략", overlay=false, process_orders_on_close = true)
left = input.int(5, "left")
right = input.int(5, "right")
// 1. 지표
rsi = ta.rsi(close, 14)
plot(rsi, linewidth = 2)
// 2. 피봇 찾기 (상승div-피봇로우, 하락div-피봇하이)
pivotlow = ta.pivotlow(rsi, left, right)
pivothigh = ta.pivothigh(rsi, left, right)
// plot(pivotlow, color = color.green, linewidth = 3, style = plot.style_linebr, offset = -5)
// 3. 다이버전스 찾기
// regular 상승다이버전스 => 지표 피봇저점은 상승, 가격 저점은 하락 
prev_pivotlow = ta.valuewhen(pivotlow, pivotlow, 1)
osc_higher_low =  pivotlow ? pivotlow > prev_pivotlow : false 
// bgcolor(osc_higher_low ? color.rgb(76, 175, 79, 90) : na, offset = -5)
prev_low = ta.valuewhen(pivotlow, low[right], 1)
price_lower_low = pivotlow ? low[right] < prev_low : false
// bgcolor(price_lower_low ? color.rgb(255, 153, 0, 90) : na, offset = -5)
is_regular_bullish_divergence = osc_higher_low and price_lower_low
// bgcolor(is_regular_bullish_divergence ? color.green : na, offset = -5)
plotshape(is_regular_bullish_divergence ? pivotlow : na, offset = -right, title="상승 다이버전스", text = "Bull", style = shape.labelup, color = color.green, textcolor = color.white, location = location.absolute)
plot(pivotlow ? pivotlow : na, color = is_regular_bullish_divergence ? color.green : na, offset = -right, linewidth = 2)

// hidden 상승다이버전스 => 지표 피봇저점은 하락, 가격 저점은 상승
osc_lower_low = pivotlow ? pivotlow < prev_pivotlow : false
price_higher_low = pivotlow ? low[right] > prev_low : false
is_hidden_bullish_divergence = osc_lower_low and price_higher_low
// bgcolor(is_hidden_bullish_divergence ? color.green : na, offset = -5)
plotshape(is_hidden_bullish_divergence ? pivotlow : na, offset = -right, title="히든 상승 다이버전스", text = "H Bull", style = shape.labelup, color = color.green, textcolor = color.white, location = location.absolute)
plot(pivotlow ? pivotlow : na, color = is_hidden_bullish_divergence ? color.green : na, offset = -right, linewidth = 2)

// regular 하락다이버전스 => 지표 피봇고점은 하락, 가격 고점은 상승 
prev_pivothigh = ta.valuewhen(pivothigh, pivothigh, 1)
osc_lower_high = pivothigh ? pivothigh < prev_pivothigh : false
prev_high = ta.valuewhen(pivothigh, high[right], 1)
price_higher_high = pivothigh ? high[right] > prev_high : false
is_regular_bearish_divergence = osc_lower_high and price_higher_high
// bgcolor(is_regular_bearish_divergence ? color.red : na, offset = -5)
plotshape(is_regular_bearish_divergence ? pivothigh : na, offset = -right, title="하락 다이버전스", text = "Bear", style = shape.labeldown, color = color.red, textcolor = color.white, location = location.absolute)
plot(pivothigh ? pivothigh : na, color = is_regular_bearish_divergence ? color.red : na, offset = -right, linewidth = 2)

// hidden 하락다이버전스 => 지표 피봇고점은 상승, 가격 고점은 하락
osc_higher_high = pivothigh ? pivothigh > prev_pivothigh : false
price_lower_high = pivothigh ? high[right] < prev_high : false
is_hidden_bearish_divergence = osc_higher_high and price_lower_high
plotshape(is_hidden_bearish_divergence ? pivothigh : na, offset = -right, title="히든 하락 다이버전스", text = "H Bear", style = shape.labeldown, color = color.red, textcolor = color.white, location = location.absolute)
plot(pivothigh ? pivothigh : na, color = is_hidden_bearish_divergence ? color.red : na, offset = -right, linewidth = 2)

// 4. 전략 진입 | 종료
is_long = is_regular_bullish_divergence
is_long_close = is_regular_bearish_divergence
is_short = is_regular_bearish_divergence

if is_long
    strategy.entry("롱", strategy.long)
if is_long_close
    strategy.close("롱")
